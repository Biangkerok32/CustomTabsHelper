apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 2
        versionName "1.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'CustomTabsHelper'

    pubGroupId = 'com.kimjio'
    libraryName = 'CustomTabsHelper'
    pubArtifactId = 'customtabs'

    gitUrl = 'https://github.com/Kimjio/CustomTabsHelper.git'

    developerId = 'kimji'
    developerName = 'Ji O Kim'
    developerEmail = 'kimjioh0927@gmail.com'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // Add sources.jar
                artifact sourcesJar

                // You can then customize attributes of the publication as shown below.
                groupId = pubGroupId
                artifactId = pubArtifactId
                version = android.defaultConfig.versionName
            }
        }
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")

        publications = ['release']
        pkg {
            repo = bintrayRepo
            name = bintrayName
            dryRun = false
            publish = true
            override = false
            publicDownloadNumbers = true
            licenses = ['Apache-2.0']
            version {
                name = android.defaultConfig.versionName
            }
        }
    }
}